# 1.1.8 ブラックボックス抽象化としての手続き

再帰とは「手続のなかに**自分自身の手続が定義されている手続**のこと」

例) 1.1.7におけるsqrt-iter

再帰について1.2でさらに詳しくやる感じです.

#### sqrtプログラムの手続分解

    sqrt
    │
    sqrt-iter
    │
    ├ improve  
    │ │  
    │ └ average
    │  
    good-enough?
    │
    ├ abs  
    │
    square

sqrtプログラムにおいて,
**自然にいくつかの処理に分割されて手続が行われている**ことに注目.

sqrtプログラムは**手続の群れ**として見ることができる.

#### ブラックボックスとしてみる

good-enough?手続を使用するときにはsquareの手続を行っているが,
squareの手続の中身を知らなくてもgood-enough?の手続を使用できる.

good-enough?手続において,square手続のことを**ブラックボックス**と考えることができる.

(ブラックボックス: 内部の動作原理や構造を理解していなくても,
外部から見た機能や使い方のみを知っていれば,
十分に得られる結果を利用する事のできる機構の概念)

#### 手続の抽象化

先ほどの考え方からgood-enough?手続において,
square手続が

*どのような計算をするのか*が大事ではなく,
***どのような結果を返すのか*** が重要であると考えることができる.

このように考えたとき,
good-enough?手続について考えるとき,

「**squareは手続ではなく手続の抽象に過ぎない**」

といえる.

#### ローカル名
まず2つの手続を示す.

    (define (good-enough? guess x)
      (< (abs (- (square guess) x))
        0.001))

    (define (square x) (* x x))

このように定義されている際に,good-enough?を実行した場合,
squareの実行によってgood-enough?内のxに影響は及ばない.

よってsquareで定義されているxと,good-enough?に定義されているxは,
別のものである.

つまり形式パラメタ(C言語で言う関数の引数)はどのような名前になってもよいと言える.

そのような名前を**束縛変数(bound variable)**と呼ぶ.

また手続を定義することはその形式パラメタを**束縛する(binds)**と呼ぶ.

変数が束縛されていなければ**自由(free)**であると表現できる.

そして,束縛変数(bound variable)の有効範囲のことを**スコープ(scope)**と呼ぶ.

(※good-enough?ではguessとxは束縛変数であり,<,-,abs,squareは自由であると言える)

#### 内部定義とブロック構造

    (define (sqrt x)
      (sqrt-iter 1.0 x))

    (define (sqrt-iter guess x)
      (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))

    (define (good-enough? guess x)
      (< (abs (- (square guess) x)) 0.001))

    (define (improve guess x)
      (average guess (/ x guess)))

sqrtの手続は多くの手続の積み重ねによってできている.
これは一つ一つバラバラになっているが,まとめることもできる.

    (define (sqrt x)
      (define (good-enough? guess x)
        (< (abs (- (square guess) x)) 0.001))
      (define (improve guess x)
        (average guess (/ x guess)))
      (define (sqrt-iter guess x)
        (if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x) x)))
    (sqrt-iter 1.0 x))

このようにdefineを入れ子にした構造を**ブロック構造(block structure)**と呼び,
これは最も単純な名前をまとめる仕組みであるといえる.

ブロック構造にすることによる利点は多々ある.
- それまで使用されていた名前を別用途で使えるようになる.名前が空く.
  - 例えばimproveという手続きは今までsqrtの手続きに最適化されていたが,
  sqrtをブロック構造に組み替えたことで,
  別の処理にimproveという名前をつけることができる.
- 補助手続の単純化ができる(これから記述する)

補助手続の単純化とはsqrtを例に取ると,*xの省略*が可能になるということである.

    (define (sqrt x)
      (define (good-enough? guess)
        (< (abs (- (square guess) x)) 0.001))
      (define (improve guess)
        (average guess (/ x guess)))
      (define (sqrt-iter guess)
        (if (good-enough? guess)
          guess
          (sqrt-iter (improve guess))))
    (sqrt-iter 1.0))

xはsqrtの定義に束縛されているが,
sqrt内で定義されている手続はxの有効範囲ないであることから,
xを明示的に渡す必要は無いと気づけるだろう.

xは内部定義で自由変数とされ,
sqrtが呼ばれた際の引数から値を受け取る.

このようなやり方(定義の仕方)を**静的有効範囲(lexical scoping)(レキシカルスコープ)**という.
