#1.1.7例:ニュートン法による平方根
以上の説明から、手続きは普通の数学の関数によく似ていることがわかりました。  
しかし、数学の関数と計算機の手続きには重要な違いも存在します。  
計算機の手続きは手順を書く必要があります。  

例として平方根を考えます。  
xの平方根をyとすると以下の式が成り立ちます。
```
y ≧ 0, y^2 = x であるような y
```
しかし、これではどのようにして平方根を求めるのか不明です。  
日本語からLispに書き換えてもそれは同じです。
```scheme
(define (sqrt x)
  (the y (and (>= y 0)
              (= (square y) x))))
```

このように、数学では宣言的な記述(what is)を行い、コンピュータサイエンスでは手続き的な記述(how to)を行うことが多いです。

では、どのように平方根を求めるのでしょうか。

一般的な方法として、ニュートン法があります。
ニュートン法はxの平方根の推定値としてyがあった場合、yとy/xの平均を取ることでより良い推定値を得ることができます。
x = 2の推定値をy = 1として計算してみましょう。

y = 1, x/y = 2/1 = 2, よって平均は(1+2)/2 = 1.5となりy = 1.5に更新  
y = 1.5, x/y = 2/1.5 = 1.3333, よって平均は(1.5+1.3333)/2 = 1.4167となりy = 1.4167に更新  
y = 1.4167, x/y = 2/1.4167 = 1.4118, よって平均は(1.4167+1.4118)/2 = 1.4142となりy = 1.4142  

と、計算を続けていくことでより良い近似値を得られることができます。

では、この手続を形式化してみます。
guessは上の例で示したyで推定値を表します。

```scheme
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))
```

sqrt-iterという手続きを定義し、もし十分な近似が得られたらguessを答えにます。  
まだ充分でない場合、improveという手続きでguessの精度を良くしてもう一度計算をします。  
ここから各手続きを定義していきます。  
まずはimproveから。  
推定値と商の平均で改善することが出来ました。
```scheme
(define (improve guess x)
  (average guess (/ x guess)))
```
ここで出てきたaverageも定義していきます。
```scheme
(define (average x y)
(/ (+ x y) 2))
```
以上でimproveの定義が完成しました。  
次にgood-enough?について、どのくらいの精度が良ければ「良い精度」であるかを決定します。  
ここでは推定値の二乗とxの差が0.001になれば良い精度であるとします。
```scheme
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

ここまで定義しましたが、sqrt-iterはxの他にguessの初期値も渡してあげる必要があります。  
これでは若干面倒なので初期値を1と決め、sqrt手続きを作成します。
```scheme
(define (sqrt x)
  (sqrt-iter 1.0 x))
```

以上で平方根を求めるプログラムが完成です。
```scheme
(sqrt 9)
```
```
3.00009155413138
```
