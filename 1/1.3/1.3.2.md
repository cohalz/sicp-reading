#lambdaを用いた手続きの構築
1.3.1で，sumを使用する際にpi-termやpi-nextのように簡単な手続きを定義しました．  
しかしこれは高階手続きで引数として使うためだけに定義したのでとても不格好です．  
"入力値に4を足す手続き"や"入力値と入力値に2を足した数の席の逆数を返す手続き"を直接指定できたら便利です．  
これは手続きを作成する特殊形式lambdaを用いて実現可能です．  
例を見てみましょう．
```scheme
(lambda (x) (+ x 4))
```
そして，
```scheme
(lambda (x) (/ 1.0 (* x (+ x 2))))

```
のように表せます．

次にpi-sum手続きをlambdaを用いて書き直します．
```scheme
(define (pi-sum a b)
  (sum (lambda (x) (/ 1.0 (* x (+ x 2))))
       a
       (lambda (x) (+ x 4))
       b))
```
さらに，integral手続きをadd-dxを定義せず書くことも可能です．
```scheme
(define (integral f a b dx)
  (* (sum f
          (+ a (/ dx 2.0))
          (lambda (x) (+ x dx))
          b)
      dx))
```

一般的にlambdaはdefineと同様に手続きを作成する事ができますが，手続きに名前がつかないことが異なります．
```
(lambda ([formal-parameters]) ([body]))
```

例えば以下の様な手続きを考えます．
```scheme
(define (plus4 x) (+ x 4))
```
この式はlambdaを用いて以下のように書き換えられます．
```scheme
(define plus4 (lambda (x) (+ x 4)))
```

#ローカル変数使用のためにletを用いる
lambdaの別の使用法として，ローカル変数の作成があります．
```math
f(x,y) = x(1+xy)^2  + y(1-y) + (1+xy)(1-y)
```
この式には(1+xy)と(1-y)が複数箇所に現れているのでこれをまとめて以下の表現にします．
```
a = 1 + xy
b = 1 - y
f(x,y) = xa^2 + yb + ab
```
このようにxとyだけでなく中間の値としてaとbをローカル変数として含むと見やすいです．  
これをSchemeで実現するために, 補助手続きをローカル変数を束縛するために使用するという方法があります．
```scheme
(define (f x y) 
  (define (f-helper a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))
  (f-helper (+ 1 (* x y))
            (- 1 y)))
```
これは数式に直すと
```
f(a,b) = xa^2 + yb + ab
```
となっており，a,bに1 + xy, 1 - yを代入して計算をしています．

これをlambdaで書きなおすことも可能です．
```scheme
(define (f x y)
  ((lambda (a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))
  (+ 1 (* x y))
  (- 1 y)))
```
このように書くとfは単一の手続きの呼び出しとなります．

このような構成はとても良く使うので，letと呼ばれる特殊形式が用意されています．  
letを用いるとf手続きは以下のようになります．
```scheme
(define (f x y)
  (let ((a (+ 1 (* x y)))
        (b (- 1 y)))
    (+ (* x (square a))
       (* y b)
       (* a b))))
```

let式の一般的な形式は以下のようになります．
```scheme
(let (([var1] [exp1])
      ([var2] [exp2])
      ...
      ([varn] [expn]))
  [body])
```

わかりやすく書くと以下の形になります．
```
let [var1] have the value [exp1] and
let [var2] have the value [exp2] and
...
let [varn] have the value [expn] 
in [body]
```
let式の最初の部分が名前と式のペアのリストであり，[body]部分でその名前を使用する事ができます．

let式は以下のlambdaの文法として評価されます．
```scheme
((lambda ([var1] ... [varn])
    [body])
  [exp1] 
  ...
  [expn])
```
exp1手続きをvar1に，expn手続きをvarnに束縛することができていることがわかります．  
このため，let式は単にlambda手続きの構文糖というだけでしかありません．  
さらにここから，letにて作成された変数のスコープは[body]部分だけであることがわかります．  
このため以下の二点に注意が必要です．  
- letは変数を可能な限り局地的に束縛します．  
- 変数の値はletの外側で計算されます．
