# 汎用手法としての手続き

この節では2つの複雑な例( **零と関数の不動点を見つけるための汎用手法** )
について考える.

## 例1: 半区間手法による方程式の根のを求める.

※方程式の根: 代数方程式 f(x) = 0 の解を特に根と呼ぶ.

### 半区間手法(harf-interval method)

#### 以下の条件を満たすfを定める.

1. fを連続関数とする
2. f(a) < 0 < f(b) となる点a, 点bを決める.
3. fは最低でも一つの0をaとbの間に持つ.

#### 方法

- aとbの平均をxとして定め, f(x)を計算.
  - f(x) > 0 ならば fは 0をaとxの間にもつ.
  - f(x) < 0 ならば fは 0をbとxの間にもつ.
- これ利用してfが0を持つ小さな区間を特定する.
- 区間が十分に小さな時点にたどり着いたら終了.

#### 実装
以下を定義.
```scheme
; 関数f, 負の数, 正の数を引数に取る
(define (search f neg-point pos-point)
  ; midpointに平均値を割り当てる
  (let ((midpoint (average neg-point pos-point)))
    ; close-enough?条件に合うかどうか評価する
    (if (close-enough? neg-point pos-point)
      ; 合うときはmidpointを返す
      midpoint
      ; 合わない時はtest-valueに(f midpoint)をわりあてて,
      (let ((test-value (f midpoint)))
      ; test-valueの正負を判定.結果に従ってループor抜ける.
        (cond ((positive? test-value)
              ; 正のとき 正の数だったところをmidpointにしてループ
               (search f neg-point midpoint))
              ; 負のとき 負の数だったところをmidpointにしてループ
              ((negative? test-value)
               (search f midpoint pos-point))
              ; どれでもないときmidpointを返す.
              (else midpoint))))))
```

close-enough?を定義
```scheme
(define (close-enough? x y) (< (abs (- x y)) 0.001))
```

searchを使いやすくするためにharf-interval-methodを定義
```scheme
(define (harf-interval-method f a b)
  ; 各valueを割り当てる
  (let ((a-value (f a))
        (b-value (f b)))
    ; aとbの正負を判定してそれに合うようにsearch
    (cond ((and (negative? a-value) (positive? b-value))
           (search f a b))
          ((and (negative? b-value) (positive? a-value))
           (search f b a))
          ; searchに合わなかったらエラー
          (else (error "Values are not of opposite sign" a b)))))
```

#### 実例

```scheme
(define (average x y)(/ (+ x y) 2))
```
とした.

##### 問題1
- sin(x) = 0の2と4の間の根を求める.

```scheme
(harf-interval-method sin 2.0 4.0)
```

##### 結果1
```zsh
gosh> (harf-interval-method sin 2.0 4.0)
3.14111328125
```

##### 問題2
- x^3 -2x -3 = 0において, 1と2の間の根をもとめる

```scheme
(harf-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                      1.0
                      2.0)
```

##### 結果2
```zsh
gosh> (harf-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                            1.0
                            2.0)
1.89306640625
```
